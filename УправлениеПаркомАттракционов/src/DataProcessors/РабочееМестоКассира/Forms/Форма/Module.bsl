#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура БаллыКСписаниюПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокНоменклатуры
&НаКлиенте
Процедура СписокНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ДанныеСтроки = Элемент.ТекущиеДанные;

	Если ДанныеСтроки.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;

	ДобавитьПозицию(ДанныеСтроки.Аттракцион, ДанныеСтроки.Цена);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)

	ДанныеСтроки = Элемент.ТекущиеДанные;

	Если ДанныеСтроки.ЭтоГруппа Тогда
		Выполнение = Ложь;
		Возврат;
	КонецЕсли;

	Значение = Новый Структура;
	Значение.Вставить("Аттракцион", ДанныеСтроки.Аттракцион);
	Значение.Вставить("Цена", ДанныеСтроки.Цена);
	
	ПараметрыПеретаскивания.Значение = Значение;
	
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиСобытийЭлементовТаблицыФормыПозицииПродажи

&НаКлиенте
Процедура ПозицииПродажиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	СтандартнаяОбработка = Ложь;
	Значение = ПараметрыПеретаскивания;
	
	Если ТипЗнч(Значение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	ДобавитьПозицию(Значение.Аттракцион, Значение.Цена);

КонецПроцедуры

&НаКлиенте
Процедура ПозицииПродажиАттракционПриИзменении(Элемент)

	ДанныеСтроки = Элементы.ПозицииПродажи.ТекущиеДанные;
	
	ДанныеСтроки.Цена = ПолучитьЦенуНоменклатуры(ДанныеСтроки.Номенклатура);	
	
	РассчитатьСуммуСтроки(ДанныеСтроки);	

КонецПроцедуры

&НаКлиенте
Процедура ПозицииПродажиЦенаПриИзменении(Элемент)

	РассчитатьСуммуСтроки(Элементы.ПозицииПродажи.ТекущиеДанные);	

КонецПроцедуры

&НаКлиенте
Процедура ПозицииПродажиКоличествоПриИзменении(Элемент)

	РассчитатьСуммуСтроки(Элементы.ПозицииПродажи.ТекущиеДанные);	

КонецПроцедуры

&НаКлиенте
Процедура ПозицииПродажиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ЗаписатьПродажу(Команда)
	НовыйДокумент = ЗаписатьПродажуНаСервере();
	ОповеститьОбИзменении(НовыйДокумент);
	ПоказатьОповещениеПользователя("Создан документ", 
		ПолучитьНавигационнуюСсылку(НовыйДокумент),
		Строка(НовыйДокумент));
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьПозицию(Аттракцион, Цена)

	Фильтр = Новый Структура;
	Фильтр.Вставить("Номенклатура", Аттракцион);

	НайденныеСтроки = ПозицииПродажи.НайтиСтроки(Фильтр);

	Если НайденныеСтроки.Количество() <> 0 Тогда
		Возврат;
	КонецЕсли;

	Строка = ПозицииПродажи.Добавить();
	Строка.Номенклатура = Аттракцион;
	Строка.Цена = Цена;
	Строка.Сумма = Цена;
	Строка.Количество = 1;
	
	РассчитатьСуммуДокумента();

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦенуНоменклатуры(Номенклатура)

	Возврат РегистрыСведений.ЦеныНоменклатуры.ЦенаНоменклатуры(Номенклатура)

КонецФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ДанныеСтроки)
	
	ДанныеСтроки.Сумма = ДанныеСтроки.Цена * ДанныеСтроки.Количество;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	СуммаИтого = ПозицииПродажи.Итог("Сумма") - БаллыКСписанию;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьПродажуНаСервере()

	ДокОбъект = Документы.ПродажаБилета.СоздатьДокумент();
	
	ДокОбъект.Заполнить(Неопределено);
	
	ДокОбъект.Дата = ТекущаяДатаСеанса();
	ДокОбъект.Клиент = Клиент;
	ДокОбъект.БаллыКСписанию = БаллыКСписанию;
	ДокОбъект.СуммаДокумента = СуммаИтого;
	
	ДокОбъект.ПозицииПродажи.Загрузить(ПозицииПродажи.Выгрузить());
	
	Если НЕ ДокОбъект.ПроверитьЗаполнение() Тогда
		ВызватьИсключение "Не удалось записать продажи";
	КонецЕсли;
	
	ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат ДокОбъект.Ссылка;
		
КонецФункции

#КонецОбласти